repositories {
  mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def getPropertyValue(String property) {
  try {
    Properties properties = new Properties()
    properties.load(new FileInputStream("local.properties"))
    return "\"" + properties.getProperty(property) + "\""
  } catch (ignored) {
    return "\"\"";
  }
}

android {
  compileSdkVersion 29

  defaultConfig {
    applicationId "au.com.codeka.weather"
    minSdkVersion 19
    targetSdkVersion 29
    multiDexEnabled true
  }

  compileOptions {
    coreLibraryDesugaringEnabled true
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      buildConfigField "String", "WUNDERGROUND_API_KEY", getPropertyValue("wundergroundApiKey")
      buildConfigField "String", "OPENWEATHERMAP_API_KEY", getPropertyValue("openWeatherMapApiKey")
      buildConfigField "String", "GOOGLE_API_KEY", getPropertyValue("googleApiKey")
    }
    debug {
      buildConfigField "String", "WUNDERGROUND_API_KEY", getPropertyValue("wundergroundApiKey")
      buildConfigField "String", "OPENWEATHERMAP_API_KEY", getPropertyValue("openWeatherMapApiKey")
      buildConfigField "String", "GOOGLE_API_KEY", getPropertyValue("googleApiKey")
    }
  }
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'androidx.palette:palette:1.0.0'
  implementation 'androidx.work:work-runtime:2.3.4'
  implementation 'com.google.android.gms:play-services-maps:17.0.0'
  implementation 'com.google.code.gson:gson:2.8.6'
  implementation('com.github.florent37:materialviewpager:1.2.3@aar') {
    transitive = true
  }

  implementation "androidx.core:core-ktx:1.2.0"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  wearApp project(':wear')
}
